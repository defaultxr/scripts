#!/usr/bin/env fish
# sshot - take a screenshot.
# avoids the junk appearing in the screenshot when scrot is used in selection mode.
# also automatically copies the path to the screenshot to the clipboard (pass -C to prevent this).
# Requirements: scrot, slop ( https://github.com/naelstrof/slop )

# usage:
# sshot [-s] [-C]

# examples:

# select a region and take a screenshot of it:
# sshot -s

# code:

argparse -n sshot 'h/help' 's/select' 'C/no-clipboard' -- $argv

if test -n "$_flag_h"
    echo "sshot - Take a screenshot."
    echo "Usage: sshot [arguments]"
    echo
    echo "  -h/--help         - Print help and exit."
    echo "  -s/--select       - Select a region to screenshot rather than the entire screen."
    echo "  -C/--no-clipboard - Don't copy the screenshot's path to the clipboard."
    exit
end

function filedate
    echo (date '+%Y-%m-%d-%H%M%S')
end

# using scrot for now because imagemagick's images fuck up weirdly with twitter

if test -n "$_flag_s"
    slop -f "%g " | read G
    set GX (string split '+' $G | head -1 | string trim)
    set filename ~/(filedate)_$GX.png
    # import -window root -crop $G $filename
    set sr (string split '+' $G | string split 'x')
    set sr (printf "%s,%s,%s,%s" $sr[3] (string trim "$sr[4]") $sr[1] $sr[2])
    # echo sr: $sr
    scrot -a $sr $filename
else
    set filename ~/(filedate)_full.png
    scrot -m $filename # add -p argument to capture pointer
    # import -window root $filename
end

if test -z "$_flag_C"
    echo -n $filename | c -i
end
