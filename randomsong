#!/usr/bin/env fish
# randomsong - Get N random songs from your music directory.

# Usage:
# randomsong [number]

# Examples:
#
# Get one random song from your library:
# randomsong
#
# Get five random songs:
# randomsong 5

# settings:

# The directory to scan for music.
set music_directory '/media/music/'

# The filename to write the cached list of songs to.
set cache '/tmp/songlist.txt'

# The file extensions to consider as audio files (case-insensitive).
set extensions 'aac' 'aif' 'aiff' 'flac' 'm4a' 'mp3' 'ogg' 'opus' 'wav' 'wma'

# code:

set -- script "$(path basename (status filename))"

argparse -n "$script" 'h/help' 'n/number=' -- $argv

if set -q _flag_h
    echo "$script - Output N random songs from your music directory ($music_directory)."
    echo "Usage: $script [arguments] [number of songs to get]"
    echo
    echo "  -h/--help   - Print help and exit."
    echo "  -n/--number - The number of songs to output."
    exit
end

set num '1'

if set -q _flag_n
    set num "$_flag_n"
else if test (count $argv) -gt 0
    set num "$argv[1]"
end

# Use bfs by default because it is faster.
set find_command (command -s bfs; or command -s find)

function find_args
    set -l index 0
    for ext in $extensions
        if not test "$index" = '0'
            echo -- -or
        end
        echo -- -iname
        echo -- "*.$ext"
        set index (math $index + 1)
    end
end

function ensure_song_list_exists -d "Generate the song list cache. This may not be needed if your drive is fast enough (this seems like it's probably the case for most SSDs/NVMes)."
    if not test -e "$cache"
        echo "Generating song list cache..." >&2
        command $find_command "$music_directory" -type f \( (find_args) \) >"$cache" 2>/dev/null
    end
end

function get_random_songs
    # ensure_song_list_exists
    # LC_ALL=C is used here because it greatly increases the speed at which sort completes.
    # Since we are just getting random lines, we don't actually care about anything locale-related.
    command $find_command "$music_directory" -type f \( (find_args) \) 2>/dev/null | LC_ALL=C sort --random-sort | head -$num
end

get_random_songs
