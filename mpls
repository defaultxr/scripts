#!/usr/bin/env fish

# mpls - list tracks in mpd's current playlist.
# includes the position in the playlist by default (unlike mpc)
# with one argument, only shows the current track and 10 before and after it.
# also shows an indicator next to the current track.

# usage:
# mpls [context-only]

# examples:
#
# list the whole playlist:
# mpls
#
# show the current song and 10 before and after it:
# mpls c
#
# show the current song and the 20 before and after it:
# mpls 20

# configuration:

# text to show in front of the current track
set prepend 'â–¶ '

# code:

set mpc (mpc --format='%position% - ')
set formatargs "--format=%position% - %artist% - %title%"
set num 10

if test (count $argv) -gt 0; and string match -qr '^\d+$' $argv[1]
    set num $argv[1]
end

if test (count $mpc) -gt 1
    set mpc (echo $mpc[1])
    if test (count $argv) -eq 1
        mpc $formatargs playlist | grep -i -A $num -B $num "^$mpc" | sed "s/^$mpc/$prepend$mpc/"
    else
        mpc $formatargs playlist | sed "s/^$mpc/$prepend$mpc/"
    end
else # mpd is not playing, so just list the whole playlist.
    if test (count $argv) -ne 1
        mpc $formatargs playlist
    else
        echo "MPD is not playing, so there is no playlist context."
    end
end
