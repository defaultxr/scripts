#!/usr/bin/env fish
# translate-selected - translate the selected text, showing the result in a popup dialog.
# Requirements: translate-shell, xclip, text-gui (part of defaultxr/scripts)
# See also: define-selected

set -- script "$(path basename (status filename))"

argparse -n "$script" 'h/help' 'f/full' -- $argv

if set -q _flag_h
    echo "$script - translate the selected text, showing the result in a popup dialog."
    echo "Usage: $script [arguments]"
    echo
    echo "  -h/--help - Print help and exit."
    echo "  -f/--full - Show additional information."
    exit
end

if not set -q _flag_f
    set -- brief --brief
end

set notification_id (notify-send --print-id --urgency=low --expire-time=10000 "$script" "Getting translation...")

set title "selected text translation"
set translation (xclip -o -selection primary | trans -no-ansi $brief 2>/tmp/translate-selected-err.txt | string collect)

if test -z "$translation"
    set title "translation error"
    set translation (cat /tmp/translate-selected-err.txt)
end

notify-send --replace-id="$notification_id" --urgency=low --expire-time=1 "Acquired translation."

# zenity --info --width=800 --text="$translation"
text-gui -T "$title" --text="$translation"
