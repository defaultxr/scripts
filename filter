#!/usr/bin/env raku

# filter - filter a list of filenames (stdin separated by newlines), outputting only those of a specified type.

# usage:
# filter <type>

# examples:

# list all the video files in ~/media:
# find ~/media | filter v

# list every audio file in your home directory:
# find ~/ | filter a

# code:

sub MAIN($type="audio") {
    my $func = do given $type {
        when /^a(udio)?$/ { sub ($_) { rx:i/\.(ogg|mp3|wav|flac|wma|m4a|aiff?|aac|opus)$/ } }
        when /^v(ideo)?$/ { sub ($_) { rx:i/\.(flv|mp4|avi|wmv|webm|ogv|mkv|mov|m2t|m4v)$/ } }
        when /^i(mage)?$/ { sub ($_) { rx:i/\.(gif|png|jpe?g|bmp|webp)$/ } }
        when /^d(ir(ectory)?)?$/ { sub ($_) { $_.IO.d; } }
        default {
            say "filter: unknown type \"$type\". Try instead: audio, video, image, directory";
            exit 1;
        }
    }
    for $*IN.lines() {
        if $func($_) {
            .say;
        }
    }
}
