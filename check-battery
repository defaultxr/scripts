#!/usr/bin/env fish
# check-battery - Check the battery and notify if it's low.
# May be useful to set as a cron job.

# settings:

set low_percent 20

# code:

set -- script "$(path basename (status filename))"

argparse -n "$script" 'h/help' -- $argv

if set -q _flag_h
    echo "$script - Check the battery and pop up a message on the OSD if it's low."
    echo "Usage: $script [arguments]"
    echo
    echo "  -h/--help - Print help and exit."
    exit
end

set -x DBUS_SESSION_BUS_ADDRESS "unix:path=/run/user/$(id --user)/bus"
set -x DISPLAY :0.0

set -l percent (acpi | string match -r '([0-9]+)%,?' | tail -1)

if test $percent -lt $low_percent
    notify-send -u critical -h string:x-dunst-stack-tag:check-battery "Battery low" "Currently at $percent%."
end
