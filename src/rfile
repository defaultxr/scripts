#!/usr/bin/env fish
# rfile - Output a random file within the current directory and subdirectories. Unknown arguments are forwarded to find.

set -- script "$(path basename (status filename))"

argparse -n "$script" --ignore-unknown 'h/help' 'd/depth=' 'n/number=' -- $argv

if set -q _flag_h
    echo "$script - Output a random file within the current directory and subdirectories. Unknown arguments are forwarded to find."
    echo "Usage: $script [arguments]"
    echo
    echo "  -h/--help   - Print help and exit."
    echo "  -d/--depth  - The depth of subdirectories to check, or 0 to recurse infinitely (default: 1)."
    echo "  -n/--number - The number of files to yield (default: 1)."
    exit
end

if command -qs bfs
    set find 'bfs'
else
    set find 'find'
end

if set -q _flag_d
    if not test "$_flag_d" = '0'
        set -- maxdepth -maxdepth "$_flag_d"
    end
else
    set -- maxdepth -maxdepth '1'
end

if set -q _flag_n
    set number "$_flag_n"
else
    set number '1'
end

command $find $maxdepth $argv | shuf | head -$number
