#!/usr/bin/env fish

# sshot - take a screenshot.
# avoids the junk appearing in the screenshot when scrot is used in selection mode.
# also automatically copies the path to the screenshot to the clipboard (pass -C to prevent this).
# requires slop - https://github.com/naelstrof/slop

# usage:
# sshot [-s] [-C]

# examples:

# select a region and take a screenshot of it:
# sshot -s

# code:

set region 0
set clipboard 1

function filedate
    echo (date '+%Y-%m-%d-%H%M%S')
end

for i in $argv
    if test $i = '-s'
        set region 1
    end
    if test $i = '-C'
        set clipboard 0
    end
end

if test $region -eq 1
    slop -f "%g" | read G
    set GX (string split '+' $G | head -1)
    set filename ~/(filedate)_$GX.png
    import -window root -crop $G $filename
else
    set filename ~/(filedate)_full.png
    import -window root $filename
end

if test $clipboard -eq 1
    echo $filename | xclip -i -selection clip
end
