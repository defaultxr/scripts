#!/usr/bin/env fish
# move a file using a file selector gui.
# accepts a list of files either as arguments or from stdin if '-' is the first argument.
# will also call 'tmsu repair' if you have tmsu in your path.
# requires zenity.

# FIX - maybe this should be able to rename files too?

argparse -n move-gui 'h/help' -- $argv

if test -n "$_flag_h"
    echo "move-gui - move a file using a file selector gui."
    echo "Usage: move-gui [files]"
    echo
    echo "  -h/--help - Print help and exit."
    echo "  -         - Read file list from stdin."
    exit
end

set script (path basename (status filename))

if test (count $argv) -eq 0; or test "$argv[1]" = '-'
    set files (cat)
else
    set files $argv
end

set count (count $files)

if test $count -eq 0
    zenity --info --title="$script Error" "No files"
    exit 1
else if test $count -eq 1
    set title "Move $(path basename $files[1]) To..."
else
    set title "Move $count Files To..."
end

set dir (realpath $files[1])

set destination (zenity --file-selection --title="$script: $title" --directory --filename="$dir")

if test $status -ne 0
    exit 0
end

for i in $files
    set full (realpath $i)
    mv $full $destination
    set basename (path basename $i)
    if command -q tmsu
        tmsu repair --manual $full "$destination/$basename" 2>/dev/null
    end
end
