#!/usr/bin/env fish

# randomsong - get a list of N random songs from your music directory.
# this caches a list of songs to a file so it only has to be generated once.
# it's recommended that the list be saved in /tmp so it will be regenerated once per boot (in case you get new songs)

# usage:
# randomsong [number]

# examples:
#
# get one random song from your library:
# randomsong
#
# get five random songs:
# randomsong 5

# configuration:

# your music directory
set directory '/media/music/'

# where to save the list of songs
set cache '/tmp/songlist.txt'

# code:

argparse -n randomsong 'h/help' -- $argv

if test -n "$_flag_h"
    echo "randomsong - get a list of N random songs from your music directory."
    echo "Usage: randomsong [arguments] [number of songs to get]"
    echo
    echo "  -h/--help - Print help and exit."
    exit
end

set num $argv[1]

if test -z "$num"
    set num 1
end

function makesonglist
    echo "Making song list..." >&2
    find "$directory" -iname '*.mp3' -or -iname '*.flac' -or -iname '*.m4a' -or -iname '*.ogg' >"$cache" 2>/dev/null
end

function rsong
    if test -e "$cache"
        sort -R "$cache" | head -$num
    else
        makesonglist
        rsong
    end
end

rsong
