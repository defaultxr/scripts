#!/usr/bin/env fish
# rg-package-files - run ripgrep on all of the files (not directories) installed by a package.

argparse -n rg-package-files 'h/help' -- $argv

if set -q _flag_h
    echo "rg-package-files - run ripgrep on all of the files (not directories) installed by a package."
    echo "Usage: rg-package-files [arguments] [package] [query]"
    echo
    echo "  -h/--help - Print help and exit."
    exit
end

set package "$argv[1]"

set query "$argv[2]"

set len (math (string length "$package") + 2)

set files

# ignore directories
for file in (pacman -Ql "$package" | string sub --start "$len")
    if not string match --quiet '*/' "$file"
        set -a files "$file"
    end
end

rg "$query" $files
